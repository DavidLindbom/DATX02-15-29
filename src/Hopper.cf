--
-- Layout
-- ======
-- Inserts "{" "}" around definition after the keywords below

layout "=", "::", "of" ;
layout toplevel        ;

--
-- Module
-- ======
--

entrypoints Module;
MMod. Module ::= "module" IdCon Exports "where" ";" [Import] [Def] ;

--
-- Exports
-- =======
-- NEmpty allows leaving the () out
-- NExp now allows exporting constructors

NEmpty. Exports ::=                  ;
NExps.  Exports ::= "(" [Export] ")" ;

NExp. Export ::= Id  ;
separator Export "," ;

--
-- Imports
-- =======
--

IImport. Import ::= "import" IdCon ;
separator Import ";" ;

--
-- Definitions
-- ===========
-- Moved out definitions to make them more flexible and reuseable

DFun.  Def ::= Func ;
DSig.  Def ::= Sign ;
DAdt.  Def ::= Adt  ;
separator Def ";"   ;

--
-- Functions
-- =========
--

FFun. Func ::= IdVar [Arg] "=" "{" Expr "}" ;

--
-- Arguments
-- =========
-- Is just a pattern but allows to be empty

APat. Arg ::= Pat ;
terminator Arg "" ;

--
-- Expressions
-- ===========
-- Renamed from Exp to Expr to remove name clash with Prelude
-- Pattern in clauses now allows 'Just _' without parenthesis

EId.      Expr2 ::= Id                                   ;
EPrim.    Expr2 ::= Prim                                 ;
EOpr.     Expr2 ::= "(" IdOpr ")"                        ;
EInfix.   Expr1 ::= Expr1 IdOpr Expr2                    ;
EApp.     Expr1 ::= Expr1 Expr2                          ;
ECase.    Expr1 ::= "case" Expr1 "of" "{" [Clause] "}"   ;
EIf.      Expr1 ::= "if" Expr1 "then" Expr2 "else" Expr2 ;
ELambda.  Expr  ::= "\\" [Pat] "->" Expr                 ;
coercions Expr 2                                         ;

CClause. Clause ::= ClausePat "->" Expr ;
separator nonempty Clause ";"           ;

CCPPat. ClausePat ::= Pat         ;
CCPCon. ClausePat ::= IdCon [Pat] ;

--
-- Patterns
-- ========
--

PId.    Pat ::= Id                 ;
PPrim.  Pat ::= Prim               ;
PWild.  Pat ::= "_"                ;
PTuple. Pat ::= "(" [PatTuple] ")" ;
terminator nonempty Pat ""         ;

PTCon. PatTuple ::= IdCon [Pat] ;
PTPat. PatTuple ::= Pat         ;
separator PatTuple ","          ;

--
-- Signatures
-- ==========
--

SSig. Sign ::= IdVar "::" "{" [Type] "}" ;

--
-- Types
-- =====
-- If TypeTuple has one element, treat it as parenthethis
-- Replace TName and TVar with TId ::= Id ?

TName.  Type ::= IdCon [Id]          ;
TVar.   Type ::= IdVar               ;
TTuple. Type ::= "(" [TypeTuple] ")" ;
separator nonempty Type "->"         ;

TTTuple. TypeTuple ::= [Type] ;
separator TypeTuple ","       ;

--
-- ADT declarations
-- ================
-- 

AAdt. Adt ::= "data" IdCon [AdtVar] "=" "{" [AdtCon] "}" ;

AVVar. AdtVar ::= IdVar ;
terminator AdtVar ""    ;

ACCon. AdtCon ::= IdCon [AdtArg] ;
separator nonempty AdtCon "|"    ;

AAId.    AdtArg ::= Id                    ;
AATuple. AdtArg ::= "(" [AdtArgTuple] ")" ;
terminator AdtArg ""                      ;

AATCon. AdtArgTuple ::= IdCon AdtArg [AdtArg] ;
AATArg. AdtArgTuple ::= AdtArg      ;
separator nonempty  AdtArgTuple "," ;

--
-- Identifiers and primitives
-- ==========================
-- To simplify lists without any separators

ICon. Id ::= IdCon ;
IVar. Id ::= IdVar ;
terminator Id ""   ;

IInteger. Prim ::= Integer ;
IDouble.  Prim ::= Double  ;
IString.  Prim ::= String  ;
IChar.    Prim ::= Char    ;

--
-- Tokens
-- ======
-- IdVar: identifiers, starting with a lower letter
-- IdCon: constructors, starting with a upper letter
-- IdOpr: operators

token IdVar (lower (digit | letter | '_' )* ) ; 
token IdCon (upper (digit | letter)* )        ;
token IdOpr ( [ ".:-^*+=<>&%$!#%|/\\" ]+ )    ;

--
-- Comments 
-- ========
-- Can't handle nested comments, bug in bnfc

comment "--"      ;
comment "{-" "-}" ;

